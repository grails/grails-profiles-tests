import org.ajoberstar.grgit.Grgit

plugins {
  id "org.ajoberstar.grgit" version "3.0.0"
  id 'base'
}

ext {
  profilesSlug = 'grails-profiles'
  profiles = ['base', 'web']
}

task cloneGrailsWrapper {
  doLast() {
    def grgit = Grgit.clone(dir: "${buildDir}/grails-wrapper".toString(),
            uri: "https://github.com/grails/grails-wrapper.git")
  }
}

task cloneGrailsCore {
  doLast() {
    def grgit = Grgit.clone(dir: "${buildDir}/grails-core".toString(),
              uri: "https://github.com/grails/grails-core.git")
  }
}

task cloneProfiles {
  doLast() {
    profiles.each { String profile ->
      def grgit = Grgit.clone(dir: "${buildDir}/${profile}".toString(),
              uri: "https://github.com/${profilesSlug}/${profile}.git")
    }
  }
}

task publishBaseProfileToMavenLocal(type: GradleBuild) {
  dependsOn cloneProfiles
  ext {
    profileName = 'base'
  }
  buildFile = "${buildDir}/${profileName}/build.gradle"
  tasks = ['publishToMavenLocal']
}

task publishWebProfileToMavenLocal(type: GradleBuild) {
  dependsOn cloneProfiles
  ext {
    profileName = 'web'
  }
  buildFile = "${buildDir}/${profileName}/build.gradle"
  tasks = ['publishToMavenLocal']
}

task publishGrailsCoreToMavenLocal(type: GradleBuild) {
  dependsOn cloneGrailsCore
  buildFile = "${buildDir}/grails-core/build.gradle"
  tasks = ['install']
}

task publishReposToMavenLocal {
  dependsOn publishBaseProfileToMavenLocal
  dependsOn publishWebProfileToMavenLocal
  dependsOn publishGrailsCoreToMavenLocal
}

task cloneRepos {
  dependsOn cloneGrailsCore
  dependsOn cloneProfiles
}

task fixTests {
  ext {
    appFolder = 'demo'
    unitTestFolder = 'src/test/groovy/'
    integrationTestFolder = 'src/integration-test/groovy/'
    unitTestName = 'Foo'
    controllerName = 'Book'
    domainName = 'Book'
    serviceName = 'Book'
    taglibName = 'Currency'
    integrationTest = 'Func'
  }

  doLast() {
    for (String spec : ["${controllerName}Controller",
                        "${controllerName}Interceptor",
                        "${taglibName}TagLib",
                        "${serviceName}Service",
                        "${domainName}",
                        "${unitTestName}"]) {
      File f = new File("${appFolder}/${unitTestFolder}/${appFolder}/${spec}Spec.groovy")
      assert f.exists()
      String text = f.text
      text = text.replace("true == false", "true == true")
      f.text = text
    }
    for (String spec : [integrationTest]) {
      File f = new File("${appFolder}/${integrationTestFolder}/${appFolder}/${spec}Spec.groovy")
      assert f.exists()
      String text = f.text
      text = text.replace("true == false", "true == true")
      f.text = text
    }
  }
}

build.dependsOn cloneGrailsWrapper
build.dependsOn cloneRepos
build.dependsOn publishReposToMavenLocal
